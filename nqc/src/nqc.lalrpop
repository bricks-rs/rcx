use std::str::FromStr;
use crate::parser::ast::{Expr, BinaryOp, Stmt};

grammar;

pub Stmt: Box<Stmt<'input>> = {
    <Expr> ";" => Stmt::Expr(<>).into(),
    FuncDecl,
}

pub Expr: Box<Expr<'input>> = {
    AddSubExpr,
};

AddSubExpr: Box<Expr<'input>> = {
    AddSubExpr AddSubOp MulDivExpr => Expr::BinaryOp(<>).into(),
    MulDivExpr,
}

pub AddSubOp: BinaryOp = {
    "+" => BinaryOp::Add,
    "-" => BinaryOp::Sub,
};

MulDivExpr: Box<Expr<'input>> = {
    MulDivExpr MulDivOp Term => Expr::BinaryOp(<>).into(),
    Term,
}

MulDivOp: BinaryOp = {
    "*" => BinaryOp::Mul,
    "/" => BinaryOp::Div,
}

pub Term: Box<Expr<'input>> = {
    Num => Expr::Literal(<>).into(),
    Ident => Expr::Ident(<>).into(),
    "(" <Expr> ")",
};

Num: i32 = r"[0-9]+" => i32::from_str(<>).unwrap();
Ident: &'input str = r"[_a-zA-Z][_a-zA-Z0-9]*";

FuncDecl: Box<Stmt<'input>> = {
    "int" <Ident> "{" <Expr> "}" => Stmt::FuncDecl(<>).into(),
}
